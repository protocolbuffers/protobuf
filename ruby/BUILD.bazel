# Protobuf Ruby runtime
#
# See also code generation logic under /src/google/protobuf/compiler/ruby.

load("@rules_pkg//:mappings.bzl", "pkg_files", "strip_prefix")
load("@rules_ruby//ruby:defs.bzl", "ruby_library", "ruby_test", "ruby_gem")
load("@upb//cmake:build_defs.bzl", "staleness_test")
load("//:protobuf.bzl", "internal_ruby_proto_library")
load("//build_defs:internal_shell.bzl", "inline_sh_test")
load("//conformance:defs.bzl", "conformance_test")

################################################################################
# Ruby Runtime
################################################################################

config_setting(
    name = "java_ruby",
    values = {"define": "ruby_platform=java"},
)

config_setting(
    name = "c_ruby",
    values = {"define": "ruby_platform=c"},
)

filegroup(
    name = "java_srcs",
    srcs = glob([
        "src/**/*.proto",
        "src/**/*.java",
        "Gemfile",
        "google-protobuf.gemspec",
        "pom.xml",
    ]),
    visibility = [
        "//ruby:__subpackages__",
    ],
)

filegroup(
    name = "rake",
    srcs = ["Rakefile"],
    visibility = [
        "//ruby:__subpackages__",
    ],
)

ruby_library(
    name = "protobuf",
    deps = ["//ruby/lib/google:protobuf_lib"],
    visibility = [
        "//visibility:public",
    ],
)

################################################################################
# Tests
################################################################################

# Define this here so the descriptor paths match what we get in Rake tests.
internal_ruby_proto_library(
    name = "test_ruby_protos",
    srcs = ["//ruby/tests:test_protos"],
    proto_deps = ["//:well_known_protos"],
    includes = [".", "src", "ruby/tests"],
    visibility = [
        "//ruby:__subpackages__",
    ],
)


conformance_test(
    name = "conformance_test",
    failure_list = "//conformance:failure_list_ruby.txt",
    testee = "//conformance:conformance_ruby",
    text_format_failure_list = "//conformance:text_format_failure_list_ruby.txt",
    target_compatible_with = select({
        ":java_ruby": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

conformance_test(
    name = "conformance_test_jruby",
    failure_list = "//conformance:failure_list_jruby.txt",
    testee = "//conformance:conformance_ruby",
    text_format_failure_list = "//conformance:text_format_failure_list_jruby.txt",
    target_compatible_with = select({
        ":java_ruby": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
)

genrule(
    name = "copy_ruby_amalgamation_h",
    srcs = ["@upb//:ruby-upb.h"],
    outs = ["generated-in/ext/google/protobuf_c/ruby-upb.h"],
    cmd = "cp $< $@",
)

genrule(
    name = "copy_ruby_amalgamation_c",
    srcs = ["@upb//:ruby-upb.c"],
    outs = ["generated-in/ext/google/protobuf_c/ruby-upb.c"],
    cmd = "cp $< $@",
)

staleness_test(
    name = "test_amalgamation_staleness",
    outs = [
        "ext/google/protobuf_c/ruby-upb.h",
        "ext/google/protobuf_c/ruby-upb.c",
    ],
    generated_pattern = "generated-in/%s",
)

################################################################################
# Distribution files
################################################################################

pkg_files(
    name = "dist_files",
    srcs = [
        "//ruby/tests:dist_files",
        "//ruby/ext/google/protobuf_c:dist_files",
        "//ruby/lib/google:dist_files",
        ":java_srcs",
        ".gitignore",
        "BUILD.bazel",
        "internal.bzl",
        "Gemfile",
        "Rakefile",
        "README.md",
        "travis-test.sh",
    ],
    strip_prefix = strip_prefix.from_root(""),
    visibility = ["//pkg:__pkg__"],
)
