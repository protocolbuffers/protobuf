# Protobuf IO library.

load("@rules_cc//cc:defs.bzl", "cc_library", "cc_test")
load("@rules_pkg//:mappings.bzl", "pkg_files", "strip_prefix")
load("//build_defs:cpp_opts.bzl", "COPTS", "LINK_OPTS")

package(
    default_visibility = ["//visibility:public"],
)

cc_library(
    name = "io",
    srcs = [
        "coded_stream.cc",
        "zero_copy_stream.cc",
        "zero_copy_stream_impl.cc",
        "zero_copy_stream_impl_lite.cc",
    ],
    hdrs = [
        "coded_stream.h",
        "zero_copy_stream.h",
        "zero_copy_stream_impl.h",
        "zero_copy_stream_impl_lite.h",
    ],
    copts = COPTS,
    include_prefix = "google/protobuf/io",
    deps = [
        ":io_win32",
        "//:arena",
        "//src/google/protobuf/stubs:lite",
    ],
)

cc_library(
    name = "printer",
    srcs = ["printer.cc"],
    hdrs = ["printer.h"],
    copts = COPTS + select({
        "//build_defs:config_msvc": [],
        "//conditions:default": ["-Wno-maybe-uninitialized"],
    }),
    include_prefix = "google/protobuf/io",
    deps = [
        ":io",
        "//src/google/protobuf/stubs",
    ],
)

cc_library(
    name = "tokenizer",
    srcs = [
        "strtod.cc",
        "tokenizer.cc",
    ],
    hdrs = [
        "strtod.h",
        "tokenizer.h",
    ],
    copts = COPTS,
    include_prefix = "google/protobuf/io",
    deps = [
        ":io",
        "//src/google/protobuf/stubs",
    ],
)

cc_library(
    name = "gzip_stream",
    srcs = ["gzip_stream.cc"],
    hdrs = ["gzip_stream.h"],
    copts = COPTS + select({
        "//build_defs:config_msvc": [],
        "//conditions:default": ["-Wno-maybe-uninitialized"],
    }),
    include_prefix = "google/protobuf/io",
    deps = [
        ":io",
        "//src/google/protobuf/stubs",
    ] + select({
        "//build_defs:config_msvc": [],
        "//conditions:default": ["@zlib//:zlib"],
    }),
)

cc_library(
    name = "io_win32",
    srcs = ["io_win32.cc"],
    hdrs = ["io_win32.h"],
    copts = COPTS,
    include_prefix = "google/protobuf/io",
    visibility = ["//pkg:__pkg__"],
    deps = [
        "//:arena",
        "//src/google/protobuf/stubs:lite",
    ],
)

cc_test(
    name = "io_test",
    srcs = [
        "coded_stream_unittest.cc",
        "printer_unittest.cc",
        "tokenizer_unittest.cc",
        "zero_copy_stream_unittest.cc",
    ],
    copts = COPTS + select({
        "//build_defs:config_msvc": [],
        "//conditions:default": ["-Wno-maybe-uninitialized"],
    }),
    data = [
        "//:testdata",
    ],
    deps = [
        ":gzip_stream",
        ":io",
        "//:protobuf",
        "//src/google/protobuf/testing",
        "@com_google_googletest//:gtest",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_test(
    name = "win32_test",
    srcs = ["io_win32_unittest.cc"],
    tags = [
        "manual",
        "windows",
    ],
    deps = [
        "//:protobuf_lite",
        "@com_google_googletest//:gtest",
        "@com_google_googletest//:gtest_main",
    ],
)

################################################################################
# Distribution packaging
################################################################################

pkg_files(
    name = "dist_files",
    srcs = glob(["**/*"]),
    strip_prefix = strip_prefix.from_root(""),
    visibility = ["//pkg:__pkg__"],
)
